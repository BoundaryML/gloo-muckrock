@function ExtractRequestData {
  @input string
  @output FOIARequestData
}

@class FOIARequestData {
  trackingNumber string?
  dateEstimate string?
  price float?
  priceDetails string?
  reasoning string
  requestStatus RequestStatus
  recordsStatus RecordsStatus
}

@enum RecordsStatus {
  NOT_APPLICABLE
  RECORDS_FOUND
  NO_RECORDS_FOUND 
  MORE_RECORDS_PENDING
}
 
@enum RequestStatus {
  IN_PROGRESS 
  FIX_REQUIRED
  PAYMENT_REQUIRED
  REQUEST_REJECTED
  REQUEST_COMPLETED
  INDETERMINATE
}

@variant[llm] v1 for ExtractRequestData {
  @client[llm] GPT4Client

  @stringify FOIARequestData {
    dateEstimate
    @describe {
      An ISO8601 string of the date by which the agency estimates it will complete processing the request. Use null if not present.
    }

    price
    @describe {
      The total balance remaining to pay for this request, as a float. Use null if not present.
    }

    priceDetails
    @describe {
      A sentence describing what the price is for (e.g. per page or record delivered, etc). null if not present.
    }
  }

  @stringify RequestStatus {
    IN_PROGRESS
    @describe{
      The agency accepted the request and is working on it. Use this status if the agency still has more work to do or has records pending, and the status doesn't fit any other status.
    }

    FIX_REQUIRED
    @describe{
      The agency has asked the requestor for clarification, to supply additional information, to narrow down a request, or complete an additional task (not including forwarding the request to another agency or interacting with online portal systems) in order to allow them to continue processing the request
    }

    PAYMENT_REQUIRED
    @describe {
      The requestor must pay a fee for the agency to continue processing.
    }

    REQUEST_REJECTED
    @describe {
      The request has been denied due to legal exemptions, an issue with the request such as it not being specific enough, the request should be forwarded to another agency, or another reason which causes them not to be able to process it.
    }

    REQUEST_COMPLETED
    @describe{
      The agency has completed their processing of the request, and has attached any responsive documents (possibly redacted) found, if there are any. Only use this if there is nothing else left to do for either party.
    }

    INDETERMINATE
    @describe {
      Use this status if you cannot determine the correct status from the information provided or if the communication is not related to a public records request.
    }
  }

  @stringify RecordsStatus {
    MORE_RECORDS_PENDING
    @describe{
      The agency has indicated that there are more records to be released soon for this request
    }

    NO_RECORDS_FOUND
    @describe {
      The agency has indicated that there are no records found for this request
    }

    RECORDS_FOUND
    @describe {
      The agency has indicated that there are records found for this request
    }

    NOT_APPLICABLE
    @describe{
      The text does not match any other record status. It may be that the request is still in progress.
    }
  }

  @prompt {
    You are analyzing public records correspondence to figure out what the status of the request for public records is. Your job is to extract the information from the government's response and classify the status of the request.
         
    For the requestStatus field, use the following definitions:
    {@RequestStatus.values}
 
    For the recordsStatus field, use the following definitions:
    {@RecordsStatus.values}

    Agency's response to user:
    ###
    {@input}
    ###

    OUTPUT FORMAT:
    {@output.json}

    JSON:
  }
}