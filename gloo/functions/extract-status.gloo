@function ExtractRequestData {
  @input string
  @output FOIARequestData
}

@class FOIARequestData {
  trackingNumber string
  dateEstimate string
  price int 
  reasoning string
  requestStatus RequestStatus
  recordsStatus RecordsStatus
}

@enum RecordsStatus {
  NOT_APPLICABLE
  RECORDS_FOUND
  NO_RECORDS_FOUND 
  MORE_RECORDS_PENDING
}
 
@enum RequestStatus {
  IN_PROGRESS
  FIX_REQUIRED
  PAYMENT_REQUIRED
  REQUEST_REJECTED
  REQUEST_COMPLETED
  PENDING_MORE_DOCS
  INDETERMINATE
}

@variant[llm] v1 for ExtractRequestData {
  @client[llm] GPT4Client

  @stringify RequestStatus {
    IN_PROGRESS
    @describe{
      The agency accepted the request and is now working on it.
    }

    FIX_REQUIRED
    @describe{
      The agency has asked the requestor for clarification, to supply additional information, to narrow down a request, or complete an additional task in order to allow them to continue processing the request
    }



    PAYMENT_REQUIRED
    @describe {
      The requestor must pay a fee for the agency to continue processing.
    }

    REQUEST_REJECTED
    @describe {
      The request has been denied or needs to be re-routed elsewhere, and no search for records was done.
    }

    REQUEST_COMPLETED
    @describe{
      The text indicates that a response to the public record request is now attached or completed and some records were found (even if they are redacted). If there is nothing else for the agency or the user to do, then this is the correct status
    }

    PENDING_MORE_DOCS
    @describe{
      The agency has indicated that there are more records to be released soon for this request.
    }

    INDETERMINATE
    @describe {
      Use this status if you cannot determine the correct status from the information provided.
    }
  }

  @stringify RecordsStatus {
    MORE_RECORDS_PENDING
    @describe{
      The agency has indicated that there are more records to be released soon for this reques
    }

    NO_RECORDS_FOUND
    @describe {
      The agency has indicated that there are no records found for this request
    }

    RECORDS_FOUND
    @describe {
      The agency has indicated that there are records found for this request
    }

    NOT_APPLICABLE
    @describe{
      The text does not match any other record status. It may be that the request is still in progress.
    }
  }

  @prompt {
    You are analyzing public records correspondence to figure out what the status of the request for public records is. Your job is to extract the information from the government's response and classify the status of the request.
         
    For the {@input.requestStatus} field, use the following definitions:
    {@RequestStatus.values}

    For the {@input.recordsStatus} field, use the following definitions:
    {@RecordsStatus.values}

    Agency's response to user:
    ###
    {@input}
    ###

    OUTPUT FORMAT:
    {@output.json}

    JSON:
  }
}